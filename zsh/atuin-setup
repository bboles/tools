# vim: set syntax=sh filetype=sh:

atuin-setup() {
  if ! which atuin &>/dev/null; then return 1; fi
  bindkey '^E' _atuin_search_widget

  export ATUIN_NOBIND="true"
  eval "$(atuin init zsh)"
  fzf-atuin-history-widget() {
    local selected num
    setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2>/dev/null

    # local atuin_opts="--cmd-only --limit ${ATUIN_LIMIT:-5000}"
    local atuin_opts='--format "{time}\t{command}"'
    local fzf_opts=(
      --height=${FZF_TMUX_HEIGHT:-50%}
      --tac
      --nth=2..
      --tiebreak=index
      "--query=${LBUFFER}"
      "+m"
      "--bind=ctrl-d:reload(atuin search $atuin_opts -c $PWD),ctrl-r:reload(atuin search $atuin_opts),ctrl-p:toggle-preview"
      --preview='echo {3..}'
      --preview-window=down:10,wrap,hidden
      --tabstop=3
      --border=double
    )

    selected=$(
      eval "atuin search ${atuin_opts}" |
        fzf "${fzf_opts[@]}" |
        awk -F'\t' '{print $2}'
    )
    local ret=$?
    if [ -n "$selected" ]; then
      # the += lets it insert at current pos instead of replacing
      LBUFFER+="${selected}"
    fi
    zle reset-prompt
    return $ret
  }
  zle -N fzf-atuin-history-widget
  bindkey '^R' fzf-atuin-history-widget
}
